# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/ets_frontend/ts2panda/ts2abc_config.gni")
import("//build/ohos.gni")
import("//commonlibrary/ets_utils/ets_utils_config.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("//foundation/arkui/ace_engine/build/ace_gen_obj.gni")

# compile .ts to .js.
action("build_ts_js") {
  script = "//commonlibrary/ets_utils/js_api_module/build_ts_js.py"

  args = [
    "--dst-file",
    rebase_path(target_out_dir + "/js_buffer.js"),
    "--module-path",
    rebase_path("/commonlibrary/ets_utils/js_api_module/buffer"),
    "--out-file",
    rebase_path("./out/js_buffer.js"),
    "--relative-path",
    rebase_path("//", root_build_dir),
  ]

  outputs = [ target_out_dir + "/js_buffer.js" ]
}

config("buffer_config") {
  cflags_cc = [
    "-std=c++17",
    "-Wno-deprecated-declarations",
  ]
}

base_output_path = get_label_info(":js_buffer", "target_out_dir")
gen_obj("js_buffer") {
  input = "$target_out_dir/js_buffer.js"
  if (use_mac || use_mingw_win || use_ios || use_linux) {
    js_buffer_obj_path = base_output_path + "/buffer.c"
  } else {
    js_buffer_obj_path = base_output_path + "/buffer.o"
  }
  output = js_buffer_obj_path
  snapshot_dep = [ ":build_ts_js" ]
}

# compile .js to .abc.
action("gen_buffer_abc") {
  visibility = [ ":*" ]
  script = "//arkcompiler/ets_frontend/ts2panda/scripts/generate_js_bytecode.py"

  args = [
    "--src-js",
    rebase_path(target_out_dir + "/js_buffer.js"),
    "--dst-file",
    rebase_path(target_out_dir + "/buffer.abc"),
    "--node",
    rebase_path("${node_path}"),
    "--frontend-tool-path",
    rebase_path("${ts2abc_build_path}"),
    "--node-modules",
    rebase_path("${node_modules}"),
    "--module",
  ]
  deps = [
    ":build_ts_js",
    "//arkcompiler/ets_frontend/ts2panda:ark_ts2abc_build",
  ]

  inputs = [ target_out_dir + "/js_buffer.js" ]
  outputs = [ target_out_dir + "/buffer.abc" ]
}

abc_output_path = get_label_info(":buffer_abc", "target_out_dir")
gen_obj("buffer_abc") {
  input = "$target_out_dir/buffer.abc"
  if (use_mac || use_mingw_win || use_ios || use_linux) {
    buffer_abc_obj_path = abc_output_path + "/buffer_abc.c"
  } else {
    buffer_abc_obj_path = abc_output_path + "/buffer_abc.o"
  }
  output = buffer_abc_obj_path
  snapshot_dep = [ ":gen_buffer_abc" ]
}

ohos_shared_library("buffer") {
  include_dirs = [
    "//third_party/icu/icu4c/source/common",
    "//third_party/node/src",
    "//foundation/arkui/napi/interfaces/kits",
    "//commonlibrary/ets_utils/js_api_module/buffer",
  ]

  sources = [
    "converter.cpp",
    "js_blob.cpp",
    "js_buffer.cpp",
    "native_module_buffer.cpp",
  ]

  deps = [
    ":gen_obj_src_buffer_abc",
    ":gen_obj_src_js_buffer",
    "//foundation/arkui/napi/:ace_napi",
    "//third_party/bounds_checking_function:libsec_shared",
    "//third_party/icu/icu4c:shared_icuuc",
  ]

  if (use_mingw_win) {
    deps += [ "$hilog_windows" ]
  } else if (use_linux) {
    deps += [ "$hilog_linux" ]
  } else if (use_mac) {
    deps += [ "$hilog_mac" ]
  } else {
    external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
  }

  configs = [ ":buffer_config" ]
  subsystem_name = "commonlibrary"
  part_name = "ets_utils"

  relative_install_dir = "module"
}

ohos_source_set("buffer_static") {
  include_dirs = [
    "//third_party/icu/icu4c/source/common",
    "//third_party/node/src",
    "//foundation/arkui/napi/interfaces/kits",
    "//commonlibrary/ets_utils/js_api_module/buffer",
  ]

  sources = [
    "converter.cpp",
    "js_blob.cpp",
    "js_buffer.cpp",
    "native_module_buffer.cpp",
  ]

  external_deps = [ "c_utils:utils" ]
  deps = [
    ":gen_obj_src_buffer_abc",
    ":gen_obj_src_js_buffer",
    "//foundation/arkui/napi/:ace_napi",
    "//third_party/icu/icu4c:shared_icuuc",
  ]

  configs = [ ":buffer_config" ]

  if (is_standard_system) {
    external_deps += [ "hiviewdfx_hilog_native:libhilog" ]
  } else {
    external_deps += [ "hilog:libhilog" ]
  }

  if (is_cross_platform_build) {
    include_dirs += [
      "$plugins_root/hilog/include",
      "$plugins_root/interfaces",
    ]
    if (target_os == "android") {
      defines = [ "ANDROID_PLATFORM" ]
    }
    if (target_os == "ios") {
      deps -= [ "//third_party/icu/icu4c:shared_icuuc" ]
      deps += [ "//third_party/icu/icu4c:static_icuuc" ]
    }
  }
  subsystem_name = "commonlibrary"
  part_name = "ets_utils"
}

group("buffer_packages") {
  public_deps = [ ":buffer" ]
}
